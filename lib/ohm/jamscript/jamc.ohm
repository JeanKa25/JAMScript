JAMC <: C {
    Source          = External_decl+

    Namespace_spec  = jame id ";" // This looks rather like a typedef'd global decl...

    Jcond_specifier = "{" Jcond_expr "}"

    Jcond_expr      = "(" Jcond_expr ")"                            -- paran
                    | "!" Jcond_expr                                -- not
                    | Jcond_expr jcond_expr_op Jcond_expr           -- bin_op
                    | id "." id                                     -- namespace
                    | id                                            -- id

    jcond_expr_op   = "&&" | "||"

    Async_activity  = jtaskAsync Jcond_specifier? Jtask_attr_spec? id "(" Jamparam_type_lst? ")" Namespace_spec? Compound_stmt

    Sync_activity   = jtaskSync Jamtype_return Jcond_specifier? Jtask_attr_spec? id "(" Jamparam_type_lst? ")" Namespace_spec? Compound_stmt

    Jtask_attr_spec = "[" ListOf<Jtask_attr, ",">  "]"

    Jtask_attr      = type "=" (sync | realtime | batch)
                    | period "=" number
                    | jitter "=" number
                    | reuse "=" (true | false)
                    | reuse_limit "=" decimalInt
                    | reuse_buffer "=" decimalInt
                    | reuse_function "=" id

    Jamtype_return  = Jamtype
                    | void

    Jamtype         = ( unsigned? int | unsigned )                     -- Int
                    | unsigned? long unsigned? long unsigned? int?     -- LongLongInt
                    | unsigned? char                                   -- Char
                    | string_type ( "(" decimalInt ")" )?              -- String
                    | float                                            -- Float
                    | double                                           -- Double

    Jamparam_type_lst = NonemptyListOf<Jamparam_decl, ",">

    Jamparam_decl   = Jamtype id ( "[" decimalInt "]" )?

    Activity_def    = Sync_activity
                    | Async_activity

    External_decl   := Activity_def
                    |  Namespace_spec

    keyword         := jtaskAsync | jtaskSync | jname // TODO? | string_type ?

    string_type = "string" ~identPart
    jtaskAsync = "jtask*" ~identPart
    jtaskSync = "jtask" ~identPart
    jname = "jname" ~identPart
    type = "type" ~identPart
    period = "period" ~identPart
    jitter = "jitter" ~identPart
    reuse = "reuse" ~identPart
    reuse_limit = "reuse_limit" ~identPart
    reuse_buffer = "reuse_buffer" ~identPart
    reuse_function = "reuse_function" ~identPart
    batch = "BATCH" ~identPart
    sync = "SYNC" ~identPart
    realtime = "REALTIME" ~identPart
    true = "true" ~identPart
    false = "false" ~identPart
}