JAMC <: C {
    Source         := External_decl+

    Namespace_spec  = in id

    Type_spec       += jamtask

    Jexport         = jexport id ";"

    Jrequire        = jrequire id as id at ("fog" | "cloud" | "device") ";"

    Jcond_specifier = "{" Jcond_expr "}"

    Jcond_expr      = "(" Jcond_expr ")"                            -- paran
                    | "!" Jcond_expr                                -- not
                    | Jcond_expr jcond_expr_op Jcond_expr           -- bin_op
                    | id "." id                                     -- namespace
                    | id                                            -- id

    jcond_expr_op   = "&&" | "||"

    Async_activity  = jtaskAsync Jcond_specifier? Declarator Namespace_spec? Compound_stmt

    Sync_activity   = jtaskSync Decl_specs Jcond_specifier? Declarator Namespace_spec? Compound_stmt
    
    Jtask_attr_spec = "[" ListOf<Jtask_attr, ",">  "]"
    
    Jtask_attr      = type "=" (sync | realtime | batch)
                    | period "=" number
                    | jitter "=" number
                    | reuse "=" bool_expr
                    | reuse_limit "=" number
                    | reuse_argdev "=" percentage
                    | max_controllers "=" number
                    
   	percentage 	    = number "%"
    
    Jtask_activity  = Decl_specs jtask Jcond_specifier? Jtask_attr_spec? Declarator Compound_stmt

    Activity_def    = Sync_activity
                    | Async_activity
                    | Jtask_activity

    External_decl   :=  Jexport 
                    | Jrequire
                    | Activity_def
                    | Prototype
                    | Function_def
                    | Decl
                    | Preprocess_line

    as = "as" ~identPart
    at = "at" ~identPart
    jtaskAsync = "jtask*" ~identPart
    jtaskSync = "jtask" ~identPart
    jtask = "jtask" ~identPart
    in = "in" ~identPart
    jamtask = "jamtask" ~identPart
    jexport = "jexport" ~identPart
    jrequire = "jrequire" ~identPart
    type = "type" ~identPart
    period = "period" ~identPart
    jitter = "jitter" ~identPart
    reuse = "reuse" ~identPart
    reuse_limit = "reuse_limit" ~identPart
    reuse_argdev = "reuse_argdev" ~identPart
    max_controllers = "max_controllers" ~identPart
    batch = "BATCH" ~identPart
    sync = "SYNC" ~identPart
    realtime = "REALTIME" ~identPart
    bool_expr = "true" | "false"
}