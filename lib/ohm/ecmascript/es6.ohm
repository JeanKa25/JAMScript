ES6 <: ES5 {
  AssignmentExpression<guardIn> += ArrowFunction<guardIn>

  ArrowFunction<guardIn> = ArrowParameters<guardIn> #(spacesNoNL "=>") ConciseBody<guardIn>

  ConciseBody<guardIn> = ~"{" AssignmentExpression<guardIn>  -- noBraces
                       | FunctionBody

  ArrowParameters<guardIn> = BindingIdentifier<guardIn>  -- unparenthesized
                           | ArrowParamsParen<guardIn>

  BindingIdentifier<guardIn> = identifier  // TODO: Implement this properly.

  ArrowParamsParen<guardIn> = "(" FormalParameterList ")"

  Declaration += AsyncFunctionDeclaration

  AsyncFunctionDeclaration = async function identifier "(" FormalParameterList ")" FunctionBody

  CallExpression += await CallExpression --awaitCall

  Statement += ForAwaitOfStatement
            |  ForAwaitOfDeclStatement

  ForAwaitOfStatement = for await "(" LeftHandSideExpression of Expression<withIn> ")" Statement

  ForAwaitOfDeclStatement = for await "(" (var | const | let) VariableDeclaration<noIn> of Expression<withIn> ")" Statement

  keyword += await | async

  await = "await" ~identifierPart
  async = "async" ~identifierPart
}
